<job id="Configure-SQLServerMemory">
<script language="VBScript" src="..\..\scripts\ZTIUtility.vbs"/>
<script language="VBScript"> 

'//----------------------------------------------------------------------------
'// Solution: Hydration
'// Purpose: Used to set maximum memory in SQL 2012
'// Usage: Configure-SQLServerMemory [/debug:true]
'// Version: 1.0 - September 10, 2012 - Johan Arwidmark
'//
'// This script is provided "AS IS" with no warranties, confers no rights and 
'// is not supported by the authors or Deployment Artist. 
'//
'//----------------------------------------------------------------------------

'//----------------------------------------------------------------------------
'// Global constant and variable declaration
'//---------------------------------------------------------------------------- 

Option Explicit 

Dim iRetVal 

'//----------------------------------------------------------------------------
'// End declarations
'//---------------------------------------------------------------------------- 

'//----------------------------------------------------------------------------
'// Main routine
'//---------------------------------------------------------------------------- 

'On Error Resume Next
iRetVal = ZTIProcess
ProcessResults iRetVal
On Error Goto 0 

'//---------------------------------------------------------------------------
'//
'// Function: ZTIProcess()
'//
'// Input: None
'// 
'// Return: Success - 0
'// Failure - non-zero
'//
'// Purpose: Perform main ZTI processing
'// 
'//---------------------------------------------------------------------------
Function ZTIProcess() 

    Dim sSQLMaximumMemory
    Dim sSQLConfigurationFile
    Dim oSQLConfigurationFile
    Dim sSQLConfigurationFile2

    oLogging.CreateEntry "Configure SQL Server Memory: Starting", LogTypeInfo	
    oLogging.CreateEntry "Configure SQL Server Memory: Current server memory is: " & oEnvironment.Item("Memory") & " MB", LogTypeInfo	

    ' If more than 16 GB of RAM, set SQL Server Maximum Memory to 70 percent, otherwise 50 percent
    If oEnvironment.Item("Memory") > 15900 Then 
        oLogging.CreateEntry "Configure SQL Server Memory: More than 16 GB Memory found, setting SQL Server Maximum Memory to 70 percent of server memory", LogTypeInfo	
        sSQLMaximumMemory = Int("0.7" * oEnvironment.Item("Memory"))
    Else 
        oLogging.CreateEntry "Configure SQL Server Memory: Less than 16 GB Memory found, setting SQL Server Maximum Memory to 50 percent of server memory", LogTypeInfo	
        sSQLMaximumMemory = Int("0.5" * oEnvironment.Item("Memory"))
    End if

    sSQLConfigurationFile = oShell.ExpandEnvironmentStrings("%temp%") & "\SetMaximumMemory.sql"

    ' Create SQL Script
	Set oSQLConfigurationFile = oFSO.CreateTextFile(sSQLConfigurationFile, True, False)
	TestAndFail SUCCESS, 6721, "Open" & sSQLConfigurationFile
	oSQLConfigurationFile.WriteLine "EXEC sp_configure 'show advanced options', '1'"
	oSQLConfigurationFile.WriteLine "RECONFIGURE WITH OVERRIDE"
	oSQLConfigurationFile.WriteLine "EXEC sp_configure 'max server memory', '" & sSQLMaximumMemory & "'"
	oSQLConfigurationFile.WriteLIne "RECONFIGURE WITH OVERRIDE" 
    oSQLConfigurationFile.WriteLine "EXEC sp_configure 'show advanced options', '0'"
	oSQLConfigurationFile.WriteLIne "RECONFIGURE WITH OVERRIDE" 
	oSQLConfigurationFile.Close

    ' Run the SQL Script using OSQL, output to logfile
    oLogging.CreateEntry "Configure SQL Server Memory: Setting SQL Server Maximum Memory to: " & sSQLMaximumMemory & " MB", LogTypeInfo	    
    oShell.Run """C:\Program Files\Microsoft SQL Server\150\Tools\Binn\osql"" -S localhost -E -i""" & sSQLConfigurationFile & """ " & "-o """ & oUtility.LocalRootPath & "\SMSOSD\OSDLOGS\Configure-SQLServerMemory_osql_output.log"""

End Function



</script>
</job>
