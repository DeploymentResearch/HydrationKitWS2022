<job id="Install-ConfigMgr">
<script language="VBScript" src="..\..\scripts\ZTIUtility.vbs"/>
<script language="VBScript"> 

'//----------------------------------------------------------------------------
'// Solution: Hydration
'// Purpose: Used to install ConfigMgr 
'//
'// This script is provided "AS IS" with no warranties, confers no rights and 
'// is not supported by the authors or Deployment Artist. 
'//
'//----------------------------------------------------------------------------

'//----------------------------------------------------------------------------
'// Global constant and variable declaration
'//---------------------------------------------------------------------------- 

Option Explicit 

Dim iRetVal 

'//----------------------------------------------------------------------------
'// End declarations
'//---------------------------------------------------------------------------- 

'//----------------------------------------------------------------------------
'// Main routine
'//---------------------------------------------------------------------------- 

'On Error Resume Next
iRetVal = ZTIProcess
ProcessResults iRetVal
On Error Goto 0 

'//---------------------------------------------------------------------------
'//
'// Function: ZTIProcess()
'//
'// Input: None
'// 
'// Return: Success - 0
'// Failure - non-zero
'//
'// Purpose: Perform main ZTI processing
'// 
'//---------------------------------------------------------------------------
Function ZTIProcess() 

	Dim sFile
	Dim sCmd
	
	' Install ConfigMgr Primary site

	oLogging.ReportProgress "Installing ConfigMgr", 20

	sFile = oUtility.ScriptDir & "\Source\SMSSETUP\BIN\X64\setup.exe"
	If not oFSO.FileExists(sFile) then
		oLogging.CreateEntry "Install ConfigMgr: " & sFile & " was not found, unable to install ConfigMgr", LogTypeError
		ZTIProcess = Failure
		Exit Function
	End if

	' Workaround for bug in ConfigMgr / MDT, where ConfigMgr setup deletes the registry info the MDT task sequence needs
	oLogging.CreateEntry "Exporting Task Sequence Registry Info", LogTypeInfo
	sCmd = "reg export HKLM\SOFTWARE\Microsoft\SMS C:\Windows\Temp\TS.reg"	
	oShell.Run sCmd

	' Run ConfigMgr Setup
	sCmd = """" & oUtility.ScriptDir & "\Source\SMSSETUP\BIN\X64\setup.exe""" & " /Script """ & oUtility.ScriptDir & "\ConfigMgrUnattend.ini""" & " /NoUserInput"
	iRetVal = oUtility.RunWithHeartbeat(sCmd)

	if iRetVal <> 0 then
		oLogging.CreateEntry "Install ConfigMgr: Error installing ConfigMgr, see C:\ConfigMgrSetup.log for details.", LogTypeWarning
	End If

	' Workaround for bug in ConfigMgr / MDT, where ConfigMgr setup deletes the registry info the MDT task sequence needs
	oLogging.CreateEntry "Importing Task Sequence Registry Info", LogTypeInfo
	sCmd = "reg import C:\Windows\Temp\TS.reg"	
	oShell.Run sCmd

	
End Function 

</script>
</job>